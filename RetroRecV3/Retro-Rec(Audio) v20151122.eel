//==v20151211== Reaper v5.1,SWS 2.8.2
function Create_JS_Utility()
(  GetResourcePath(#Res_Path);//Get Resouces Path
   #File_Patch = strcat(#JS_Folder_Name,#JS_FX_Name);//Concat strings
   #Full_Path = strcat(#Res_Path,#File_Patch);//Concat strings
   JS_FILE = fopen(#Full_Path, "w");//Create JSFX
////////////////////////////////////////////////////////////////////////////////////////////////
//==================JS_Utility TEXT===========================================================//
   fwrite(JS_FILE,"
desc:ForRetroRec(Audio) v20151211(b9)
options:maxmem=33554432
slider1:0<-4096,65536,0.001>Buf_Start
slider2:0<0,65536,0.001>Lenght(in sec)
slider3:1<0,4096,1>Track Number
slider4:0<0,1,1>Insert Audio
@init
ext_noinit=1;
buf=0;
//==max_buf Lenght(mltpl4!)==//
max_buf=33554432;
Thresh=10;//Its Optimal value
@slider
slider1=Buf_Start;
slider2=Lenght/srate;
Track_Num = slider3;
Insert = slider4;
@block
slider2 = Lenght/srate; 
Start_Play_Position = play_position;
i>0&&(play_state==1||play_state==5)&&abs(Start_Play_Position-Last_Play_Position)>0.125 ? Stop_Rec=1;
Last_Play_Position = Start_Play_Position;
@sample
play_state==0 ? (i=0; max_i=max_buf; Stop_Rec=0;);//RESET

play_state==1 ? (i==0 ? (maxsamples = max(abs(spl0),abs(spl1)) * 10^16;//maxsmplval*10^16
                         maxsamples>Thresh ? (Buf_Start=play_position; slider1=Buf_Start;) : Lenght=0;); 
                 //==Save each Sample to buf[]==//
                 i<=max_i && maxsamples>Thresh ? (buf[i]=spl0;buf[i+1]=spl1;
                                                  Lenght=i/2;
                                                  Stop_Rec==1 ? max_i=i;
                                                  i+=2;);
                 );
//==Mute True,else Pass True(to next JS)==//
i>max_i ? (spl0=spl0;spl1=spl1;) : (spl0=0;spl1=0;);
@gfx
Insert==1 ? 
    (Insert=0; export_buffer_to_project(buf,Lenght,2,srate,Track_Num,0,tempo); slider4=0;);", 0);
  fclose(JS_FILE);
JS_FILE;//func return
//============================================================================================//  
);//////////////////////////////////////////////////////////////////////////////////////////////

function Find_JS_Track()//==Find Track w JS-utility(uses #JS_Track_Name)==//
(i=0;loop(CountTracks(0), Cur_Track_ID = GetTrack(0,i);
 GetSetMediaTrackInfo_String(Cur_Track_ID, "P_NAME", #Curr_Track_Name, 0);
 stricmp(#JS_Track_Name,#Curr_Track_Name) == 0 ? Track_ID = Cur_Track_ID;i+=1;);
 Track_ID;//Return
);  

function Insert_JS_Track()//===Insert Track(if not found);Create JS(if not Exist)===//
( InsertTrackAtIndex(0,Defaults);TrackList_AdjustWindows(0);//Insert Track
  Track_ID = GetTrack(0, 0); GetSetMediaTrackInfo_String(Track_ID, "P_NAME", #JS_Track_Name, 1);//Get Track_ID,Set Track Name
  FX_index = TrackFX_GetByName(Track_ID,#JS_FX_Name,1);//Insert JSFX and Get FX_index 
     FX_index<0 ? (MB = MB("The desired JS-utility is not found!\nWould you like to create it?\n(In REAPER\\Effects\\utility folder)", "Info", 0);
                     //===Create JS in Reaper resoursces folder(Patch\REAPER\Effects\midi)==//
                     MB ?  (Create_JS_Utility() ? FX_index=TrackFX_GetByName(Track_ID,#JS_FX_Name,1);//Create JS//Insert JSFX//Get index 
                            FX_index==0 ? MB("Successfully!", "Info", 0) : MB("Failed...", "Info", 0););//Verify                
                   );//End Create&Insert JSFX
    //=====Set FX(aka Buffers) Count==============//
      Track_ID>0 && FX_index==0 ? 
       (SetOnlyTrackSelected(Track_ID);//Select Only! Track
        Main_OnCommandEx(NamedCommandLookup("_S&M_COPYFXCHAIN5"), 0, 0);
          GetUserInputs("Set  the  number  of  buffers", 1 , "Set number of buffers(1-50):", #Buffs);//Set
          match("%i",#Buffs, Buffs);//as Integer = Buffs
          Buffs<1 ?  Buffs=1; Buffs>50 ? Buffs=50;//If user set <1 or >100 
        loop(Buffs-1, Main_OnCommandEx(NamedCommandLookup("_S&M_COPYFXCHAIN10"), 0, 0); );
        );    
    //=====Set Specific Track_Parameters==============//
     Track_ID>0 && FX_index==0 ? 
     (SetMediaTrackInfo_Value(Track_ID,"I_RECARM",1);SetMediaTrackInfo_Value(Track_ID,"I_RECMON",1);//Rec-Arm = Enable//Rec-Monitor = Enable
      SetMediaTrackInfo_Value(Track_ID,"I_RECINPUT",Rec_IN);SetMediaTrackInfo_Value(Track_ID,"I_RECMODE",2);//Set Rec_IN and Mode=Disable(Mon Only)  
      SetMediaTrackInfo_Value(Track_ID,"B_MAINSEND",0);//No send to parent!
      SetMediaTrackInfo_Value(Track_ID,"I_SELECTED",0);//Unselect Track
         MB("Hide JS-Track?","Info",1)==1 ? //Hide TRACK in TCP&Mixer if need
           (SetMediaTrackInfo_Value(Track_ID,"B_SHOWINTCP",0);SetMediaTrackInfo_Value(Track_ID,"B_SHOWINMIXER",0););  
      TrackList_AdjustWindows(0););//Update Tracklist  
 Track_ID;//Return
);
//================MAIN=======================//
//=====Main Operations and functions=========//
function Glue_Items()
(CountSelectedMediaItems(0)>1 ? Main_OnCommandEx(40362, 0, 0);//Glue Sel Items,IF count>1
 items[Item_Number]=GetSelectedMediaItem(0,0);//Get-Save Glued_ID            
 SelectAllMediaItems(0,0);//Unsel ALL Items in Project     
 Item_Number+=1;//next            
);

function Implode_Glued_Items()
(New_Lenght=Max_Pos-Min_Pos;//Its Constant For ALL Items
 Glued_Count=Item_Number;//Value from Glue_Items() function
 Item_Number=0;
  loop(Glued_Count,
       Glued_ID=items[Item_Number];//Get Glued_ID from items[Item_Number]
       Take_ID=GetActiveTake(Glued_ID);//Get Active Take_ID from Current Glued_Item 
         Item_Start=GetMediaItemInfo_Value(Glued_ID, "D_POSITION");//Get Item Start
         Start_Offset=Min_Pos-Item_Start;//Calculate Offset for Audio
       //==Set Item Parameters==//
       SetMediaItemInfo_Value(Glued_ID, "B_LOOPSRC", 0);//LoopSource=0
       SetMediaItemInfo_Value(Glued_ID, "D_POSITION", Min_Pos);//Set New Position
       SetMediaItemTakeInfo_Value(Take_ID, "D_STARTOFFS", Start_Offset);//Set Offset for Compensate Position
       SetMediaItemInfo_Value(Glued_ID, "D_LENGTH", New_Lenght);//Set New_Lenght for Item 
            #NewName = #TrackName;
            sprintf(#Item_Number, "%d",Item_Number+1);//Num To String(and 1-based count)
            strcat(#NewName,#Item_Number);//Name + NUMBER           
            GetSetMediaItemTakeInfo_String(Take_ID,"P_NAME",#NewName,1);//Rename Item to par_Track name+Num***
       SetMediaItemSelected(Glued_ID,1);//Select Item
       Main_OnCommand(40543, 0);//Implode Selected Items into Takes 
       Item_Number+=1;
       );

UpdateArrange();//Update the arrangement 
);

function Insert_and_Close()
(TrackFX_GetParam(Track_ID,FX_index,3,minval,maxval)>0 ? defer("Insert_and_Close();") :  
                                                          (TrackFX_Show(Track_ID, FX_index, 0);//Close After Insert
                                                           FX_index+=1;//Next JS
                                                           defer("Main();"););//Call Main Function                                                
);

function Set_JS_Parameters()//==Set Parameters in JS for Insert Audio==//
(SetEditCurPos(Buf_Start, 0, 0);//Set CursPos=Buf_Start for Insert Item
  Min_Pos=min(Min_Pos,Buf_Start);//Update Min Position
  Max_Pos=max(Max_Pos,Buf_End);//Update Max Position
      TrackFX_SetParam(Track_ID, FX_index, 2, Track_Num-1);//Set Prmtr-Track number in JS
      TrackFX_SetParam(Track_ID, FX_index, 3, 1);//Set Prmtr-Insert Audio=1 in JS
      TrackFX_Show(Track_ID, FX_index, 1);//Open FX
);

function Main()//==Its Main function,it call other functions==//
(Buf_Start=TrackFX_GetParam(Track_ID,FX_index,0,minval,maxval);//Get Buf_Start from JS 
 Lenght=TrackFX_GetParam(Track_ID,FX_index,1,minval,maxval);//Get Lenght from JS
   FX_index==0 ? (Min_Pos=Buf_Start;Max_Pos=Buf_Start+Lenght;);//Initial Min-Max Positions 
   FX_index>0 && (Buf_End-Buf_Start)>0.0001 ? Glue_Items();//IF Prev_End>Current_Start Glue Prev_Selected Items
     Buf_End=Buf_Start+Lenght;//Now Update Buf_End 
 
//===Execute Insert_and_Close() func,else(if ALL Inserted) Glue last items and Implode glued into Takes===//
 FX_index<JS_FX_Count && Lenght>0 ? (Set_JS_Parameters();Insert_and_Close();) : 
                                    (Glue_Items();Implode_Glued_Items(););                             
);

//===================START======================//
//===========Basic Names and Main Data==========//
#JS_FX_Name = "ForRetroRec(Audio) v20151211(b9)";//JS-utility Name
#JS_Folder_Name = "/Effects/utility/";//Destination folder
#JS_Track_Name = #JS_FX_Name;//JS_Track_Name(the same as JS_FX_Name)
#Buffs="10";//Default Value in UserInput 
Rec_IN = 1024;//1024=Stereo Channels

//====Find_JS_Track====Insert,IF Track w JS-utility not Found====//
Find_JS_Track() == 0 ? Insert_JS_Track();//Insert Track and JS(Create JS,if need) 
 
Sel_Track_ID = GetSelectedTrack(0,0);//Get 1-st selected Track ID(For ADD AUDIO-DATA)
GetSetMediaTrackInfo_String(Sel_Track_ID, "P_NAME", #TrackName, 0);//Get Sel_Track_Name(for rename New Item-Takes)
Track_Num = GetMediaTrackInfo_Value(Sel_Track_ID, "IP_TRACKNUMBER");//Get Track Number
JS_FX_Count = TrackFX_GetCount(Track_ID);

//====Insert Audio on the Track(if exist)====//
Sel_Track_ID ? (SelectAllMediaItems(0,0); Main(););//Execute Main and all incusive functions
